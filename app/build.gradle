apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

ext.versionMajor = 00
ext.versionMinor = 00
ext.versionPatch = 01
ext.versionClassifier = null
ext.isSnapshot = false
ext.minimumSdkVersion = 17
ext.minimumScreenSize = 1
ext.maximumScreenSize = 4


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "de.netalic.peacock"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}


private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 100000000 + ext.minimumScreenSize * 10000000 + ext.maximumScreenSize * 1000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null) {
        if (ext.isSnapshot) {
            ext.versionClassifier = "SNAPSHOT"
        }
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //retrofit
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"

    //koin
    implementation 'org.koin:koin-android-viewmodel:2.0.1'
    implementation 'org.koin:koin-android:2.0.1'

    //rxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    implementation 'com.github.EhsanMashhadi:helpdroid:0.9.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    implementation ('com.alimuzaffar.lib:pinentryedittext:2.0.6') {
        exclude group: 'androidx.appcompat', module: 'appcompat'
    }
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    //test
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-inline:3.0.0"
    testImplementation 'org.koin:koin-test:2.0.1'
    testImplementation "android.arch.core:core-testing:1.1.1"
}
